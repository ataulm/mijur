buildscript {
    dependencies {
        classpath 'com.novoda:sqlite-analyzer:0.0.1-SNAPSHOT'
    }
}

apply plugin: 'android'
apply plugin: 'sqlite-access'

sqliteAccess {
    migrationsDir 'src/main/assets/migrations'
    packageName 'com.ataulm.mijur'
}

dependencies {
    compile 'com.android.support:support-v13:19.0.1'

    compile 'com.novoda:notils:2.2.5'
    compile 'com.novoda.imageloader:imageloader-core:1.6.1'
    compile 'com.novoda:sqliteprovider-core:1.0.1'

    compile 'com.squareup.retrofit:retrofit:1.2.2'
    compile 'com.netflix.rxjava:rxjava-core:0.17.1'
    compile 'com.etsy.android.grid:library:1.0.3'
    compile 'joda-time:joda-time:2.3'
}
android {
    compileSdkVersion 19
    buildToolsVersion '19.0.3'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 19
        versionCode 1419010
        versionName "0.1"
    }

    def password = ""
    if (project.hasProperty('signingPassword')) {
        password = signingPassword;
    }

    signingConfigs {
        release {
            storeFile file("$System.env.KEYSTORES/mijur.keystore")
            storePassword password
            keyAlias "mijur"
            keyPassword password
        }
    }

    buildTypes {
        final String PROVIDER_AUTHORITY_TYPE = "String";
        final String PROVIDER_AUTHORITY_KEY = "PROVIDER_AUTHORITY";

        debug {
            packageNameSuffix ".debug"
            versionNameSuffix "-debug"

            buildConfigField PROVIDER_AUTHORITY_TYPE, PROVIDER_AUTHORITY_KEY, "\"com.ataulm.mijur.debug.Provider\""
        }

        release {
            debuggable false
            jniDebugBuild false

            signingConfig signingConfigs.release

            buildConfigField PROVIDER_AUTHORITY_TYPE, PROVIDER_AUTHORITY_KEY, "\"com.ataulm.mijur.Provider\""
        }
    }
}
