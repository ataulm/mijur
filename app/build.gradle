apply plugin: 'android'
apply plugin: 'android-apt'

dependencies {
    compile 'com.android.support:support-v13:18.0.0'

    compile 'com.novoda:notils:2.1-SNAPSHOT'
    compile 'com.novoda.imageloader:imageloader-core:1.6.1'
    compile 'com.novoda:sqliteprovider-core:1.0.1-SNAPSHOT'

    compile 'com.squareup.retrofit:retrofit:1.2.2'
    compile 'com.etsy.android.grid:library:1.0.3'
    compile 'joda-time:joda-time:2.3'

    compile 'com.github.frankiesardo:icepick:2.3'
    apt 'com.github.frankiesardo:icepick-processor:2.3'
}

android {
    compileSdkVersion 19
    buildToolsVersion '19.0.0'

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 19
        versionCode 1519004
        versionName "0.0.4"
    }

    def signingPassword = ""

    gradle.taskGraph.whenReady { taskGraph ->
        if (taskGraph.hasTask(':app:assembleRelease')) {
            // Only execute when we are trying to assemble a release build
            signingPassword = System.console().readPassword("\nPlease enter password for the mijur key: ")
        }
    }

    signingConfigs {
        release {
            storeFile file("$System.env.KEYSTORES/mijur.keystore")
            storePassword signingPassword
            keyAlias "mijur"
            keyPassword signingPassword
        }
    }

    buildTypes {
        final String PROVIDER_AUTHORITY_DEBUG = "public static final String PROVIDER_AUTHORITY = \"uk.co.ataulm.mijur.debug.Provider\";\n";
        final String PROVIDER_AUTHORITY_RELEASE = "public static final String PROVIDER_AUTHORITY = \"uk.co.ataulm.mijur.Provider\";\n";

        debug {
            packageNameSuffix ".debug"
            versionNameSuffix "-debug"

            buildConfig PROVIDER_AUTHORITY_DEBUG
        }

        release {
            debuggable false
            jniDebugBuild false

            gradle.taskGraph.whenReady { taskGraph ->
                if (taskGraph.hasTask(':app:assembleRelease')) {
                    signingConfig signingConfigs.release
                }
            }

            buildConfig PROVIDER_AUTHORITY_RELEASE
        }
    }    
}